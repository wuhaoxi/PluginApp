apply plugin: 'com.android.application'

android {
    compileSdkVersion this.rootProject.android.compileSdkVersion
    buildToolsVersion this.rootProject.android.buildToolsVersion


    defaultConfig {
        applicationId 'com.wu.ft_friend'
        minSdkVersion this.rootProject.android.minSdkVersion
        targetSdkVersion this.rootProject.android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        //ARouter配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName(), AROUTER_GENERATE_DOC: "enable"]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation(this.rootProject.depsLibs.appcompat) {
        exclude group: 'androidx.fragment', module: 'fragment'
    }
    implementation this.rootProject.depsLibs.design
    implementation this.rootProject.depsLibs.recyclerview
    implementation rootProject.depsLibs.constraint
    //gson解析库
    implementation rootProject.depsLibs.gson
    //arouter库
    implementation(rootProject.depsLibs.arouterapi) {
        exclude group: 'com.android.support'
    }
    annotationProcessor rootProject.depsLibs.aroutercompiler
    //业务基础库
    implementation project(':lib_base')
    //基础UI库
    implementation project(':lib_common_ui')
    //网络请求库
    implementation project(':lib_network')
    //图片加载库
    implementation project(':lib_image_loader')
    //视频播放器组件
    implementation project(':lib_video')
}

//必须所有的库都去除，任何一个库入了fragment,都会报错
configurations.all {
    exclude group: 'androidx.fragment', module: 'fragment'
}

//引入插件
apply plugin: 'replugin-plugin-gradle'

repluginPluginConfig {
    pluginName = 'ft_friend' //定义插件名
    hostApplicationId = 'com.wu.voice' //宿主application
    hostAppLauncherActivity = "com.wu.voice.application.MainShellActivity" //宿主工程启动activity
}

//添加依赖
dependencies {
    implementation "com.qihoo360.replugin:replugin-plugin-lib:2.3.3"
    compileOnly files('libs/fragment.jar') //这个jar就是从Support-fragment中提取出来的并非特制包目的是为了骗过编译期
}


